apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.0"
    defaultConfig {
        applicationId "com.leaflc.product_flavor"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }

        /**
         * The `initWith` property allows you to copy configurations from other build types,
         * then configure only the settings you want to change. This one copies the debug build
         * type, and then changes the manifest placeholder and application ID.
         */
        staging {
            initWith debug
            manifestPlaceholders = [hostName: "internal.example.com"]
            applicationIdSuffix ".debugStaging"
        }
    }


    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            assets.srcDirs = ['src/main/assets', 'src/main/assets/']
            res.srcDirs = ['src/main/res', 'src/main/res/']
        }

        // 这里要指定productFlavor下有配置的特性，不然sync now会报错
        vivo {
            res.srcDirs = ['src/vivo/res', 'src/vivo/res/']
        }

        huawei {
            res.srcDirs = ['src/xiaomi/res', 'src/huawei/res/']
        }

    }

    flavorDimensions "push_channel", "channel"


    productFlavors {


        // 极光推送
        jpush {
            dimension "push_channel"
            manifestPlaceholders = [URL   : "https://www.jiguang.cn/push/",
                                    NUMBER: "040"]
        }

        // 小米推送
        xiaomi {
            dimension "push_channel"
            manifestPlaceholders = [URL   : "http://app.mi.com/",
                                    NUMBER: "039"]
        }

        // vivo应用商店
        vivo {
            resValue("string", "app_name", "vivo应用渠道")
            dimension "channel"
            versionCode 2
            versionNameSuffix "-minApi24"
        }

        // 小米应用商店
        huawei {
            resValue("string", "app_name", "huawei应用渠道")
            dimension "channel"
            versionCode 2
            versionNameSuffix "-minApi23"

        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
